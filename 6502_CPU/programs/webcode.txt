; Compare N, Z, and C flag results of CMP and SBC
;
; Returns with ERROR = 0 if the test passes, ERROR = 1 if the test fails
;
; Four additional memory locations are used: ERROR, N1, N2, and SNZC
; These may be located anywhere convenient in RAM
;
define ERROR $ff
define N1 $fe
define N2 $fd
define SNZC $fc
TEST:  CLD       ; just in case
      LDA #1    ; store 1 in ERROR until the test passes
      STA ERROR
      LDA #0    ; initialize N1 and N2
      STA N1
      STA N2
TEST1: LDA N1

      CMP N2    ; compare N2 to N1

      PHP       ; push CMP flags

      SEC       ; subtract N2 from N1
      SBC N2

      PHP       ; store SEC SBC flags in SNZC
      PLA
      STA SNZC
      PLA       ; pull CMP flags
      EOR SNZC
      AND #$83  ; mask N, z, and C flags
      BNE TEST2
      INC N2
      BNE TEST1
      INC N1
      BNE TEST1
      LDA #0    ; the test passes, so store 0 in ERROR
      STA ERROR
TEST2: BRK